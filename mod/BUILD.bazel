load("@rules_kotlin//kotlin:jvm.bzl", "kt_jvm_library", "kt_jvm_test")
load("//rule:remap_jar.bzl", "remap_jar")
load("//rule:apply_access_widener.bzl", "apply_access_widener")
load("//rule:extract_access_widener.bzl", "extract_access_widener")
load("//rule:merge_jar.bzl", "merge_jar")
load("//rule:generate_remap_classpath.bzl", "generate_remap_classpath")
load("//rule:decompile_jar.bzl", "decompile_jar")
load("//rule:merge_jij.bzl", "merge_jij")
load("//:properties.bzl", "mod_version", "game_version")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_java//java:defs.bzl", "java_binary", "java_library")

extract_access_widener(
    name = "access_widener_dep_intermediary",
    inputs = [
        "@maven//:net_fabricmc_fabric_api_fabric_api",
        "@maven//:com_terraformersmc_modmenu",
    ],
)

remap_jar(
    name = "remapped_deps",
    classpath = [
        "//game:remapped_client_intermediary",
    ],
    inputs = [
        "@maven//:net_fabricmc_fabric_api_fabric_api",
        "@maven//:com_terraformersmc_modmenu",
        "@maven//:eu_pb4_placeholder_api",
    ],
    mixin = True,
    remap_transitive_deps = True,
    remove_jar_in_jar = True,
    mapping = "//game:merged_mapping",
    from_namespace = "intermediary",
    to_namespace = "named",
)

extract_access_widener(
    name = "access_widener_dep_named",
    inputs = [":remapped_deps"],
)

apply_access_widener(
    name = "remapped_client_access_widened_named",
    input = "//game:remapped_client_named",
    srcs = [
        "src/main/resources/armorstand.accesswidener",
        ":access_widener_dep_named",
    ],
)

decompile_jar(
    name = "decompile_client_access_widened_named",
    inputs = [":remapped_client_access_widened_named"],
)

apply_access_widener(
    name = "remapped_server_access_widened_named",
    input = "//game:remapped_server_named",
    srcs = [
        "src/main/resources/armorstand.accesswidener",
        ":access_widener_dep_named",
    ],
)

expand_template(
    name = "fabric_mod_json",
    template = "src/main/resources/fabric.mod.json",
    substitutions = {
        "${version}": mod_version,
    },
    out = "_generated/fabric.mod.json",
)

kt_jvm_library(
    name = "mod_main_unmapped",
    srcs = glob(
        include = [
            "src/main/kotlin/**/*.kt",
            "src/main/java/**/*.java",
        ],
        allow_empty = True,
    ),
    module_name = "top_fifthlight_armorstand",
    resource_strip_prefix = "mod/_generated",
    resources = [":fabric_mod_json"],
    deps = [
        "//:kotlin_serialization",
        ":remapped_server_access_widened_named",
        ":remapped_deps",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@minecraft//:%s_server_libraries" % game_version,
    ],
)

remap_jar(
    name = "mod_main",
    inputs = [":mod_main_unmapped"],
    classpath = ["//game:remapped_client_named"],
    mapping = "//game:merged_mapping",
    mixin = True,
    from_namespace = "named",
    to_namespace = "intermediary",
)

kt_jvm_library(
    name = "mod_client_unmapped_unmerged",
    srcs = glob(
        include = [
            "src/client/kotlin/**/*.kt",
            "src/client/java/**/*.java",
        ],
        allow_empty = True,
    ),
    module_name = "top_fifthlight_armorstand",
    resource_strip_prefix = "mod/src/client/resources",
    resources = glob(
        include = ["src/client/resources/**"],
        allow_empty = True,
    ),
    deps = [
        "//:kotlin_serialization",
        "//blazerod:blazerod_dep",
        ":mod_main_unmapped",
        ":remapped_client_access_widened_named",
        ":remapped_deps",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:com_h2database_h2",
        "@minecraft//:%s_client_libraries" % game_version,
    ],
)

merge_jar(
    name = "mod_client_unmapped",
    deps = [":mod_client_unmapped_unmerged"],
    resources = {
        "src/main/resources/armorstand.accesswidener": "mod/src/main/resources/",
        "//blazerod/model/model-gltf/src/test/resources:default_models": "blazerod/model/model-gltf/src/test/resources/",
    }
)

remap_jar(
    name = "mod_client",
    inputs = [":mod_client_unmapped"],
    classpath = ["//game:remapped_client_named"],
    mapping = "//game:merged_mapping",
    mixin = True,
    from_namespace = "named",
    to_namespace = "intermediary",
)

merge_jar(
    name = "mod_unmapped",
    deps = [
        ":mod_main_unmapped",
        ":mod_client_unmapped",
    ]
)

merge_jar(
    name = "mod_without_jij",
    deps = [
        ":mod_client",
        ":mod_main",
    ],
)

merge_jij(
    name = "mod",
    visibility = ["//visibility:public"],
    input = ":mod_without_jij",
    deps = {
        "@maven//:com_h2database_h2": "com_h2database_h2:2.3.232",
        "//blazerod/model/model-base": "blazerod-model-base:=",
        "//blazerod/model/model-formats": "blazerod-model-formats:=",
        "//blazerod/model/model-gltf": "blazerod-model-formats-gltf:=",
        "//blazerod/model/model-pmd": "blazerod-model-formats-pmd:=",
        "//blazerod/model/model-pmx": "blazerod-model-formats-pmx:=",
        "//blazerod/model/model-vmd": "blazerod-model-formats-vmd:=",
        "//blazerod/render": "blazerod-render:=",
    },
)

generate_remap_classpath(
    name = "remap_classpath",
    prefix = "../../../../../../",
    deps = [":remapped_deps", "//game:remapped_client_intermediary"],
)

java_binary(
    name = "client",
    srcs = [],
    data = [
        ":remap_classpath",
        "@minecraft_assets//:assets_%s" % game_version,
        "//blazerod/model/model-gltf/src/test/resources:test_models_data",
    ],
    env = {
        "LANG": "en_US.UTF8",
    },
    runtime_deps = [
        "//rule/dev_launch_wrapper",
        "@minecraft//:%s_client_libraries" % game_version,
        ":mod_unmapped",
        "//game:remapped_client_named",
        ":remapped_deps",
        "//blazerod:blazerod_dep",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:com_h2database_h2",
        "@maven//:net_fabricmc_yarn_v2",
    ],
    main_class = "top.fifthlight.fabazel.devlaunchwrapper.DevLaunchWrapper",
    jvm_flags = [
        "-Dfabric.development=true",
        "-Dfabric.log.disableAnsi=false",
        "-Dfabric.remapClasspathFile=../../remap_classpath.txt",
        "-Ddev.launch.version=%s" % game_version,
        "-Ddev.launch.type=client",
        "-Ddev.launch.assetsPath=../+minecraft+minecraft_assets",
        "-Ddev.launch.mainClass=net.fabricmc.loader.impl.launch.knot.KnotClient",
        "-Dblazerod.debug=true",
        "-Dblazerod.debug.gui=true",
        "-Darmorstand.modelDir=blazerod/model/model-gltf/src/test/resources",
        "-Darmorstand.debug=true",
        "-Darmorstand.debug.bone=false",
        "-Darmorstand.debug.gui=false",
        "-Xmx4G",
    ],
)

java_binary(
    name = "server",
    srcs = [],
    data = [":remap_classpath"],
    runtime_deps = [
        "//rule/dev_launch_wrapper",
        "@minecraft//:%s_server_libraries" % game_version,
        ":mod_unmapped",
        "//game:remapped_server_named",
        ":remapped_deps",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:net_fabricmc_yarn_v2",
    ],
    main_class = "top.fifthlight.fabazel.devlaunchwrapper.DevLaunchWrapper",
    jvm_flags = [
        "-Dfabric.development=true",
        "-Dfabric.log.disableAnsi=false",
        "-Dfabric.remapClasspathFile=remap_classpath.txt",
        "-Ddev.launch.version=%s" % game_version,
        "-Ddev.launch.type=server",
        "-Ddev.launch.mainClass=net.fabricmc.loader.impl.launch.knot.KnotServer",
        "-Xmx4G",
    ],
)